(cond-expand
  (chicken
   (include "scm-check/common.scm")
   (include "scheme-reader/scheme-reader/core.scm")
   (include "scm-check/reader.scm")
   (include "scm-check/code-warning.scm")
   (include "scm-check/check-component/import.scm")
   (include "scm-check/check-component/if.scm")
   (include "scm-check/check-component/cond.scm")
   (include "scm-check/check-component/and.scm")
   (include "scm-check/checkers.scm"))
  (else))

(import (scheme base)
        (scheme write)
        (scheme file)
        (prefix (scm-check code-warning) w/)
        (only (srfi 1) append-map)
        (prefix (scm-check reader) schk-rdr/)
        (prefix (scm-check checkers) checkers/)
        (scheme process-context))

(define (check-file filename)
  (let-values (((code debug-info) (schk-rdr/read-super filename)))
    (append-map
      checkers/check-code
      code
      debug-info)))

(define (check-stdin output-format)
  (let loop ((position (schk-rdr/initial-position "-")))
    (let ((constructed-list (schk-rdr/read-list1 (current-input-port) position)))
      (unless (null? (car constructed-list))
        (for-each
          print-warn
          (checkers/check-code
            (caar constructed-list)
            (car (schk-rdr/position-children (list-ref constructed-list 2)))))
        (loop (cadr constructed-list))))))

(define (print-warn warn output-format)
  (let ((pos-pair (schk-rdr/position->pair (w/code-warning->pos warn)))
        (filename (schk-rdr/position->filename (w/code-warning->pos warn))))
    (display filename)
    (display ":")
    (display (car pos-pair))
    (display ":")
    (display (cdr pos-pair))
    (display ":")
    (display "W")
    (display ":")
    (display (w/code-warning->message warn))
    (newline)
    (when (and (w/code-warning->suggestion warn)
               (eq? output-format 'LONG))
      (write (w/code-warning->code warn))
      (display " =>")
      (newline)

      (for-each (lambda (x) (write x)) (w/code-warning->suggestion warn))
      (newline))))

(define (simple-optparse args)
  (let loop ((args* (cdr args))
             (filename "-")
             (output-format 'SHORT))
    (cond
      ((null? args*) (values filename output-format))
      ((string=? (car args*) "--short-output")
       (loop (cdr args*) filename 'SHORT))
      ((string=? (car args*) "--long-output")
       (loop (cdr args*) filename 'LONG))
      (else (loop (cdr args*) (car args*) output-format)))))

(define (print-help)
  (display "Usage: Scm-checker <file>")(newline)
  (display "If <file> is '-', This software reads from standard input.")(newline)
  (display "Options:")(newline)
  (display "--short-output: Output simple comment.")(newline)
  (display "--long-output: Output comment and suggestion.")(newline))

(define (main)
  (let ((args (command-line)))
    (if (null? (cdr args))
      (print-help)
      (let-values (((filename output-format) (simple-optparse args)))
        (if (string=? filename "-")
          (check-stdin output-format)
          (for-each (lambda (warn) (print-warn warn output-format))
                    (check-file filename)))))))
(main)
